buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.2.0-beta1'
    }
}

def getDate() {
    return Calendar.getInstance().getTimeInMillis();
}

allprojects {
    repositories {
        mavenCentral()
    }
}
apply plugin:  'com.android.application'

dependencies {
    compile 'com.android.support:support-v4:22.0.0'
    compile 'com.android.support:appcompat-v7:22.0.0'
    compile 'org.apache.commons:commons-compress:1.9'
    compile 'org.apache.commons:commons-compress:1.9:sources'
    compile 'commons-net:commons-net:3.3'
    compile 'junit:junit:4.12'
    compile 'joda-time:joda-time:2.7'
    compile 'com.github.zafarkhaja:java-semver:0.8.0'
    compile 'com.sksamuel.diff:diff:1.1.11'
    compile 'org.unbescape:unbescape:1.1.0.RELEASE'
    compile 'org.msgpack:msgpack:0.6.11'
    compile 'com.googlecode.juniversalchardet:juniversalchardet:1.0.3'
    compile 'org.tukaani:xz:1.0'
    compile 'ch.acra:acra:4.6.1'
}

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"
    
    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 22
        versionCode 1
        versionName "1.5.4"
    }

    signingConfigs {
        release {
            if (System.getenv("KEYSTORE_FILE") != null) {
                storeFile = file(System.getenv("KEYSTORE_FILE"))
                storePassword = System.getenv("KEYSTORE_PWD")
                keyAlias = System.getenv("KEYSTORE_ALIAS")
                keyPassword = System.getenv("KEYSTORE_ALIAS_PWD")
            }
        }
    }

    buildTypes {
        debug {
            buildConfigField "java.util.Date", "BUILD_TIME", "new java.util.Date(" + getDate() + "L)"
            buildConfigField "String", "BUILD_NAME", "\"" + System.getenv("USER") + "\"";
            minifyEnabled false
            shrinkResources false
            debuggable true
            jniDebuggable true
            zipAlignEnabled true
        }
        release {
            buildConfigField "java.util.Date", "BUILD_TIME", "new java.util.Date(" + getDate() + "L)"
            buildConfigField "String", "BUILD_NAME", "\"" + System.getenv("USER") + "\"";
            if (System.getenv("KEYSTORE_FILE") != null) {
                signingConfig signingConfigs.release
            }
        }
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
            jniLibs.srcDir 'src/org/csploit/android/jniLibs'
        }

        instrumentTest.setRoot('tests')
    }
}
