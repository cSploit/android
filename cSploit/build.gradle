buildscript {
    repositories {
        google()
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.2.0-alpha03'
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        mavenCentral()
    }
}
apply plugin:  'com.android.application'

dependencies {
    implementation 'com.android.support:support-v4:27.0.2'
    implementation 'com.android.support:appcompat-v7:27.0.2'
    implementation 'com.android.support:design:27.0.2'
    implementation 'com.android.support:preference-v7:27.0.2'
    implementation 'org.apache.commons:commons-compress:1.16.1'
    implementation 'commons-net:commons-net:3.6'
    implementation 'com.github.zafarkhaja:java-semver:0.9.0'
    implementation 'org.unbescape:unbescape:1.1.5.RELEASE'
    implementation 'org.msgpack:msgpack:0.6.12'
    implementation 'com.googlecode.juniversalchardet:juniversalchardet:1.0.3'
    implementation 'org.tukaani:xz:1.8'
    implementation 'ch.acra:acra-http:5.1.0'
    implementation 'ch.acra:acra-notification:5.1.0'
    testImplementation 'junit:junit:4.12'
}

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 27
        versionCode 4
        versionName "1.7.0-unstable"
        if(System.getenv("NIGHTLY_BUILD")) {
            versionName += "+" + System.getenv("NIGHTLY_BUILD_COMMIT").substring(0, 7)
        }
    }

    lintOptions {
        if (System.getenv("NIGHTLY_BUILD")) {
            checkReleaseBuilds false
        }
        abortOnError false
    }

    signingConfigs {
        release {
            if (System.getenv("KEYSTORE_FILE") != null) {
                storeFile = file(System.getenv("KEYSTORE_FILE"))
                storePassword = System.getenv("KEYSTORE_PWD")
                keyAlias = System.getenv("KEYSTORE_ALIAS")
                keyPassword = System.getenv("KEYSTORE_ALIAS_PWD")
            }
            return true
        }
    }

    buildTypes {
        debug {
            buildConfigField "java.util.Date", "BUILD_TIME", "new java.util.Date(" + Calendar.getInstance().getTimeInMillis() + "L)"
            buildConfigField "String", "BUILD_NAME", "\"" + System.getenv("USER") + "\""
            minifyEnabled false
            shrinkResources false
            debuggable true
            jniDebuggable true
            zipAlignEnabled true
            multiDexEnabled true
        }
        release {
            buildConfigField "java.util.Date", "BUILD_TIME", "new java.util.Date(" + Calendar.getInstance().getTimeInMillis() + "L)"
            buildConfigField "String", "BUILD_NAME", "\"" + System.getenv("USER") + "\""
            if (System.getenv("KEYSTORE_FILE") != null) {
                signingConfig signingConfigs.release
            }
            multiDexEnabled true
            return true
        }
    }
}
